plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'

    // Spring Native Build
    id 'org.springframework.experimental.aot' version '0.10.3'
    id 'org.graalvm.buildtools.native' version '0.9.3'
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("build/classes/main/")
        testOutputDir = file('build/classes/java/test')
        downloadJavadoc = false
        downloadSources = true
    }
}

group = 'com.github.zxbu'
version = '2.4.0'
sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    maven {
        url 'https://maven.aliyun.com/repository/public/'
    }
    maven {
        url 'https://maven.aliyun.com/repository/spring/'
    }
    maven {
        url 'https://repo.spring.io/release'
    }
    maven {
        url 'https://jitpack.io'
    }
    mavenCentral()
}

dependencies {
    compileOnly 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.7.0'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

test {
    useJUnitPlatform()
}

// Spring Native Build

bootBuildImage {
    builder = 'paketobuildpacks/builder:tiny'
    environment = [
            'BP_NATIVE_IMAGE': 'true'
    ]
}

nativeBuild {
    classpath processAotResources.outputs, compileAotJava.outputs
}

nativeTest {
    classpath processAotTestResources.outputs, compileAotTestJava.outputs
}
